// Copyright Contributors to the OpenVDB Project
// SPDX-License-Identifier: Apache-2.0
//
#include <fvdb/BuilderResource.h>
#include <fvdb/GridBatchData.h>
#include <fvdb/detail/GridBatchDataFactory.h>
#include <fvdb/detail/ops/BuildDenseGrid.h>
#include <fvdb/detail/utils/AccessorHelpers.cuh>
#include <fvdb/detail/utils/Utils.h>
#include <fvdb/detail/utils/cuda/GridDim.h>
#include <fvdb/detail/utils/cuda/Utils.cuh>
#include <fvdb/detail/utils/nanovdb/CreateEmptyGridHandle.h>

#if CCCL_DEVICE_MERGE_SUPPORTED
#include <nanovdb/tools/cuda/DistributedPointsToGrid.cuh>
#else
#include <nanovdb/tools/cuda/PointsToGrid.cuh>
#endif

#include <c10/cuda/CUDACachingAllocator.h>
#include <c10/cuda/CUDAGuard.h>
#include <c10/cuda/CUDAMathCompat.h>
#include <torch/types.h>

#include <algorithm>
#include <limits>

namespace fvdb {
namespace detail {
namespace ops {

namespace {

__global__ __launch_bounds__(DEFAULT_BLOCK_DIM) void
ijkForDense(uint64_t offset,
            nanovdb::Coord ijkMin,
            nanovdb::Coord size,
            TorchRAcc64<int32_t, 2> outIJKAccessor) {
    const uint64_t tid = (static_cast<uint64_t>(blockIdx.x) * blockDim.x) + threadIdx.x + offset;

    if (tid >= outIJKAccessor.size(0)) {
        return;
    }

    // tid = x * size[1] * size[2] + y * size[2] + z
    const int64_t xi = tid / (size[1] * size[2]);
    const int64_t yi = tid % (size[1] * size[2]) / size[2];
    const int64_t zi = tid % size[2];

    outIJKAccessor[tid][0] = xi + ijkMin[0];
    outIJKAccessor[tid][1] = yi + ijkMin[1];
    outIJKAccessor[tid][2] = zi + ijkMin[2];
}

void
checkInputs(const torch::Device device,
            const uint32_t batchSize,
            const nanovdb::Coord &size,
            const nanovdb::Coord &ijkMin,
            const std::optional<torch::Tensor> &mask) {
    TORCH_CHECK(size[0] > 0 && size[1] > 0 && size[2] > 0,
                "Size must be greater than 0 in all dimensions");
    TORCH_CHECK((__uint128_t)size[0] * size[1] * size[2] <= std::numeric_limits<int64_t>::max(),
                "Size of dense grid exceeds the number of voxels supported by a GridBatch");
    TORCH_CHECK((__uint128_t)size[0] * size[1] * size[2] * batchSize <=
                    std::numeric_limits<int64_t>::max(),
                "Size and batch size exceed the number of voxels supported by a GridBatch");
    // The unmasked path materializes every cell as a coordinate and feeds it to NanoVDB's
    // PointsToGrid, whose radix sort casts the count to int32 (PointsToGrid.cuh:645) and silently
    // corrupts the grid above 2^31 cells (~1291^3). The masked path builds only the selected
    // subset, so it is exempt here.
    TORCH_CHECK(mask.has_value() || (__uint128_t)size[0] * size[1] * size[2] <=
                                        (__uint128_t)std::numeric_limits<int32_t>::max(),
                "Unmasked dense grid volume exceeds the 2^31-coordinate limit of the NanoVDB grid "
                "builder. Provide a sparse mask or reduce the dense dimensions.");
    if (mask.has_value()) {
        TORCH_CHECK(mask.value().device() == device,
                    "Mask device must match device of dense grid to build");
        TORCH_CHECK(mask.value().dtype() == torch::kBool, "Mask must be of type bool");
        TORCH_CHECK(mask.value().dim() == 3, "Mask must be 3D");
        TORCH_CHECK(mask.value().size(0) == size[0] && mask.value().size(1) == size[1] &&
                        mask.value().size(2) == size[2],
                    "Mask must have same size as dense grid to build");
    }
}

// Header fix-up for a buffer holding `gridCount` back-to-back copies of the same single grid:
// copy i becomes grid i of the batch. Every copy has the same mGridSize, so no offset table is
// needed. The checksum is disabled like ConcatenateGrids does (the source grid from PointsToGrid
// carries a disabled checksum anyway, so this is consistent rather than a downgrade).
__global__ void
fixupReplicatedGridHeaders(uint8_t *base, uint64_t gridSize, uint32_t gridCount) {
    const uint32_t i = blockIdx.x * blockDim.x + threadIdx.x;
    if (i >= gridCount) {
        return;
    }
    auto *data = reinterpret_cast<nanovdb::GridData *>(base + static_cast<uint64_t>(i) * gridSize);
    data->mGridIndex = i;
    data->mGridCount = gridCount;
    data->mChecksum.disable();
}

// Replicate a single-grid device handle `count` times into one buffer laid out the way
// nanovdb::cuda::mergeGridHandles lays out a multi-grid handle (grids back-to-back, each header's
// mGridIndex / mGridCount naming its slot), without the per-member allocation, host readback and
// stream synchronization that mergeGridHandles performs for every grid.
nanovdb::GridHandle<TorchDeviceBuffer>
replicateGridHandle(const nanovdb::GridHandle<TorchDeviceBuffer> &single,
                    int64_t count,
                    torch::Device device,
                    cudaStream_t stream) {
    TORCH_CHECK(single.gridCount() == 1, "replicateGridHandle expects a single-grid handle");
    TORCH_CHECK(count > 1, "replicateGridHandle expects count > 1");
    TORCH_CHECK(count <= std::numeric_limits<uint32_t>::max(), "too many grids to replicate");
    const uint8_t *src = single.buffer().deviceData();
    TORCH_CHECK(src != nullptr, "replicateGridHandle expects a device-resident handle");
    const uint64_t gridSize = single.gridSize(0);

    TorchDeviceBuffer buffer(gridSize * static_cast<uint64_t>(count), device);
    uint8_t *dst = buffer.deviceData();

    // Seed copy 0 from the source, then double the filled prefix: log2(count) device-to-device
    // memcpys instead of one per member.
    C10_CUDA_CHECK(cudaMemcpyAsync(dst, src, gridSize, cudaMemcpyDeviceToDevice, stream));
    for (int64_t filled = 1; filled < count; filled *= 2) {
        const int64_t n = std::min(filled, count - filled);
        C10_CUDA_CHECK(cudaMemcpyAsync(dst + static_cast<uint64_t>(filled) * gridSize,
                                       dst,
                                       static_cast<uint64_t>(n) * gridSize,
                                       cudaMemcpyDeviceToDevice,
                                       stream));
    }

    const int64_t numBlocks = GET_BLOCKS(count, DEFAULT_BLOCK_DIM);
    fixupReplicatedGridHeaders<<<numBlocks, DEFAULT_BLOCK_DIM, 0, stream>>>(
        dst, gridSize, static_cast<uint32_t>(count));
    C10_CUDA_KERNEL_LAUNCH_CHECK();

    // The GridHandle constructor reads grid 0's header and builds the per-grid metadata table
    // (offset = i * gridSize) from the device buffer.
    return nanovdb::GridHandle<TorchDeviceBuffer>(std::move(buffer));
}

} // namespace

template <torch::DeviceType>
nanovdb::GridHandle<TorchDeviceBuffer>
dispatchCreateNanoGridFromDense(int64_t batchSize,
                                nanovdb::Coord ijkMin,
                                nanovdb::Coord size,
                                torch::Device device,
                                const std::optional<torch::Tensor> &mask);

template <>
nanovdb::GridHandle<TorchDeviceBuffer>
dispatchCreateNanoGridFromDense<torch::kCUDA>(int64_t batchSize,
                                              nanovdb::Coord ijkMin,
                                              nanovdb::Coord size,
                                              torch::Device device,
                                              const std::optional<torch::Tensor> &mask) {
    using GridT = nanovdb::ValueOnIndex;
    TORCH_CHECK(device.is_cuda(), "device must be cuda");
    TORCH_CHECK(device.has_index(), "device must have index");
    checkInputs(device, batchSize, size, ijkMin, mask);

    c10::cuda::CUDAGuard deviceGuard(device);
    cudaStream_t stream = c10::cuda::getCurrentCUDAStream(device.index()).stream();

    const int64_t gridVolume = static_cast<int64_t>(size[0]) * size[1] * size[2];

    const int64_t NUM_BLOCKS = GET_BLOCKS(gridVolume, DEFAULT_BLOCK_DIM);

    const torch::TensorOptions opts = torch::TensorOptions().dtype(torch::kInt32).device(device);
    torch::Tensor ijkData           = torch::empty({gridVolume, 3}, opts);

    if (NUM_BLOCKS > 0) {
        ijkForDense<<<NUM_BLOCKS, DEFAULT_BLOCK_DIM, 0, stream>>>(
            0u, ijkMin, size, ijkData.packed_accessor64<int32_t, 2, torch::RestrictPtrTraits>());
        C10_CUDA_KERNEL_LAUNCH_CHECK();
    }

    if (mask.has_value()) {
        torch::Tensor maskValue = mask.value().view({-1});
        TORCH_CHECK(maskValue.device() == device, "mask must be on same device as ijkData");
        ijkData = ijkData.index({maskValue});
    }

    // This guide buffer is a hack to pass in a device with an index to the cudaCreateNanoGrid
    // function. We can't pass in a device directly but we can pass in a buffer which gets
    // passed to TorchDeviceBuffer::create. The guide buffer holds the device and effectively
    // passes it to the created buffer.
    TorchDeviceBuffer guide(0, device);

    TORCH_CHECK(ijkData.is_contiguous(), "ijkData must be contiguous");

    if (batchSize == 0) {
        // Same result as merging zero handles: an empty handle, which makeGridBatchData rejects.
        return nanovdb::GridHandle<TorchDeviceBuffer>(TorchDeviceBuffer(0, device));
    }

    // Every batch item is the same dense box (a mask, if given, is shared across the batch), so
    // build ONE grid and replicate its buffer batchSize times with a header fix-up per copy,
    // instead of one PointsToGrid (or one handle copy) per member followed by mergeGridHandles,
    // which allocates, synchronizes and reads back the host once per member.
    const int64_t nVoxels = ijkData.size(0);
    if (nVoxels == 0) {
        // Mask selected nothing: batchSize valid empty grids (built on host, moved to device).
        return createEmptyGridHandle(device, batchSize);
    }

    nanovdb::GridHandle<TorchDeviceBuffer> single = nanovdb::tools::cuda::
        voxelsToGrid<GridT, nanovdb::Coord *, TorchDeviceBuffer, BuilderResource>(
            (nanovdb::Coord *)ijkData.data_ptr(), nVoxels, 1.0, guide);
    C10_CUDA_KERNEL_LAUNCH_CHECK();

    if (batchSize == 1) {
        return single;
    }
    return replicateGridHandle(single, batchSize, device, stream);
}

template <>
nanovdb::GridHandle<TorchDeviceBuffer>
dispatchCreateNanoGridFromDense<torch::kPrivateUse1>(int64_t batchSize,
                                                     nanovdb::Coord ijkMin,
                                                     nanovdb::Coord size,
                                                     torch::Device device,
                                                     const std::optional<torch::Tensor> &mask) {
    using GridT = nanovdb::ValueOnIndex;

#if CCCL_DEVICE_MERGE_SUPPORTED
    TORCH_CHECK(device.is_privateuseone(), "device must be privateuseone");
    checkInputs(device, batchSize, size, ijkMin, mask);

    const int64_t volume = static_cast<int64_t>(size[0]) * static_cast<int64_t>(size[1]) *
                           static_cast<int64_t>(size[2]);
    const torch::TensorOptions opts = torch::TensorOptions().dtype(torch::kInt32).device(device);
    torch::Tensor ijkData           = torch::empty({volume, 3}, opts);

    for (const auto deviceId: c10::irange(c10::cuda::device_count())) {
        C10_CUDA_CHECK(cudaSetDevice(deviceId));
        cudaStream_t stream = c10::cuda::getCurrentCUDAStream(deviceId).stream();

        size_t deviceOffset, deviceVolume;
        std::tie(deviceOffset, deviceVolume) = deviceChunk(volume, deviceId);

        constexpr int64_t kNumThreads = DEFAULT_BLOCK_DIM;
        const int64_t deviceNumBlocks = GET_BLOCKS(deviceVolume, kNumThreads);
        if (deviceNumBlocks > 0) {
            ijkForDense<<<deviceNumBlocks, kNumThreads, 0, stream>>>(
                deviceOffset,
                ijkMin,
                size,
                ijkData.packed_accessor64<int32_t, 2, torch::RestrictPtrTraits>());
            C10_CUDA_KERNEL_LAUNCH_CHECK();
        }
    }

    for (const auto deviceId: c10::irange(c10::cuda::device_count())) {
        c10::cuda::getCurrentCUDAStream(deviceId).synchronize();
    }

    if (mask.has_value()) {
        torch::Tensor maskValue = mask.value().view({-1});
        TORCH_CHECK(maskValue.device() == device, "mask must be on same device as ijkData");
        ijkData = ijkData.index({maskValue});
    }

    // This guide buffer is a hack to pass in a device with an index to the cudaCreateNanoGrid
    // function. We can't pass in a device directly but we can pass in a buffer which gets
    // passed to TorchDeviceBuffer::create. The guide buffer holds the device and effectively
    // passes it to the created buffer.
    TorchDeviceBuffer guide(0, device);

    TORCH_CHECK(ijkData.is_contiguous(), "ijkData must be contiguous");

    // Every batch item is the same dense box, so build the grid once and copy it for the remaining
    // items instead of re-running DistributedPointsToGrid over the identical coordinate list.
    const int64_t nVoxels = ijkData.size(0);
    std::vector<nanovdb::GridHandle<TorchDeviceBuffer>> handles;
    handles.reserve(batchSize);
    for (int64_t i = 0; i < batchSize; i++) {
        if (!nVoxels) {
            handles.emplace_back(createEmptyGridHandle(device));
        } else if (i == 0) {
            int32_t *dataPtr = ijkData.data_ptr<int32_t>();
            auto coordPtr    = reinterpret_cast<nanovdb::Coord *>(dataPtr);

            nanovdb::cuda::DeviceMesh mesh;
            nanovdb::tools::cuda::DistributedPointsToGrid<GridT> converter(mesh);
            handles.emplace_back(
                converter.getHandle<nanovdb::Coord *, TorchDeviceBuffer>(coordPtr, nVoxels, guide));
            C10_CUDA_KERNEL_LAUNCH_CHECK();
        } else {
            handles.emplace_back(handles[0].copy(guide));
        }
    }

    for (const auto deviceId: c10::irange(c10::cuda::device_count())) {
        c10::cuda::getCurrentCUDAStream(deviceId).synchronize();
    }

    if (handles.size() == 1) {
        // If there's only one handle, just return it
        return std::move(handles[0]);
    } else {
        // This copies all the handles into a single handle -- only do it if there are multie
        // grids
        return nanovdb::cuda::mergeGridHandles(handles, &guide);
    }
#else
    TORCH_CHECK(false, "Distributed creation of grids requires CUDA 12.8 or later");
    return nanovdb::GridHandle<TorchDeviceBuffer>();
#endif
}

template <>
nanovdb::GridHandle<TorchDeviceBuffer>
dispatchCreateNanoGridFromDense<torch::kCPU>(int64_t batchSize,
                                             nanovdb::Coord ijkMin,
                                             nanovdb::Coord size,
                                             torch::Device device,
                                             const std::optional<torch::Tensor> &mask) {
    using GridT = nanovdb::ValueOnIndex;
    checkInputs(device, batchSize, size, ijkMin, mask);

    torch::TensorAccessor<bool, 3> maskAccessor(nullptr, nullptr, nullptr);
    if (mask.has_value()) {
        maskAccessor = mask.value().accessor<bool, 3>();
    }

    using ProxyGridT       = nanovdb::tools::build::Grid<float>;
    auto proxyGrid         = std::make_shared<ProxyGridT>(0.0f);
    auto proxyGridAccessor = proxyGrid->getWriteAccessor();

    for (int32_t i = 0; i < size[0]; i += 1) {
        for (int32_t j = 0; j < size[1]; j += 1) {
            for (int32_t k = 0; k < size[2]; k += 1) {
                const nanovdb::Coord ijk = ijkMin + nanovdb::Coord(i, j, k);
                if (mask.has_value()) {
                    if (maskAccessor[i][j][k] == false) {
                        continue;
                    } else {
                        proxyGridAccessor.setValue(ijk, 1.0f);
                    }
                } else {
                    proxyGridAccessor.setValue(ijk, 1.0f);
                }
            }
        }
    }

    proxyGridAccessor.merge();
    nanovdb::GridHandle<TorchDeviceBuffer> ret =
        nanovdb::tools::createNanoGrid<ProxyGridT, GridT, TorchDeviceBuffer>(
            *proxyGrid, 0u, false, false);
    ret.buffer().to(torch::kCPU);

    TorchDeviceBuffer guide(0, torch::kCPU);

    std::vector<nanovdb::GridHandle<TorchDeviceBuffer>> batchHandles;
    batchHandles.reserve(batchSize);
    batchHandles.push_back(std::move(ret));
    for (uint32_t i = 1; i < batchSize; i += 1) {
        batchHandles.push_back(batchHandles[0].copy(guide));
    }

    if (batchHandles.size() == 1) {
        return std::move(batchHandles[0]);
    } else {
        return nanovdb::mergeGrids(batchHandles);
    }
}

c10::intrusive_ptr<GridBatchData>
createNanoGridFromDense(int64_t batchSize,
                        nanovdb::Coord ijkMin,
                        nanovdb::Coord size,
                        torch::Device device,
                        const std::optional<torch::Tensor> &maybeMask,
                        const std::vector<nanovdb::Vec3d> &voxelSizes,
                        const std::vector<nanovdb::Vec3d> &origins) {
    TORCH_CHECK_VALUE(batchSize >= 0, "numGrids must be non-negative");
    if (maybeMask.has_value()) {
        TORCH_CHECK_VALUE(maybeMask.value().dtype() == torch::kBool,
                          "mask must be a boolean type or None");
        TORCH_CHECK_VALUE(maybeMask.value().dim() == 3, "mask must be 3 dimensional");
        TORCH_CHECK_VALUE(maybeMask.value().size(0) == size[0],
                          "mask must have shape (w, h, d) = denseDims");
        TORCH_CHECK_VALUE(maybeMask.value().size(1) == size[1],
                          "mask must have shape (w, h, d) = denseDims");
        TORCH_CHECK_VALUE(maybeMask.value().size(2) == size[2],
                          "mask must have shape (w, h, d) = denseDims");
    }
    TORCH_CHECK_VALUE(size[0] >= 0 && size[1] >= 0 && size[2] >= 0,
                      "denseDims must be non-negative");
    TORCH_CHECK_VALUE(batchSize <= GridBatchData::MAX_GRIDS_PER_BATCH,
                      "Cannot create a grid with more than ",
                      GridBatchData::MAX_GRIDS_PER_BATCH,
                      " grids in a batch. ",
                      "You requested ",
                      batchSize,
                      " grids.");
    auto handle = FVDB_DISPATCH_KERNEL(device, [&]() {
        return dispatchCreateNanoGridFromDense<DeviceTag>(
            batchSize, ijkMin, size, device, maybeMask);
    });
    return makeGridBatchData(std::move(handle), voxelSizes, origins);
}

} // namespace ops
} // namespace detail
} // namespace fvdb
